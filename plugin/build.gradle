// This project's rootProject is ":buildSrc", so it should have a copy of build-script settings of the "/build.gradle"
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.4.0' // https://github.com/novoda/bintray-release
    }
}
repositories {
    jcenter()
}

apply from: file('../metadata.gradle')

ext {
    versionFile = file("../VERSION")
    versionName = versionFile.readLines()[0].trim()
    metadata.version = versionName
}

apply plugin: 'groovy'
apply plugin: 'maven' // to add "plugin:install" to install it locally
apply plugin: 'com.novoda.bintray-release' // to upload it to bintray

targetCompatibility = JavaVersion.VERSION_1_8
sourceCompatibility = JavaVersion.VERSION_1_8

final metadata = ext.metadata
final ARTIFACT_ID = "license-tools-plugin"

// for "plugin:install"
group = metadata.groupId
jar.baseName = ARTIFACT_ID
version = metadata.version

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'com.android.tools.build:gradle:2.3.1'
    compile 'org.yaml:snakeyaml:1.17'
    compile 'org.apache.commons:commons-lang3:3.4'
    testCompile 'junit:junit:4.12'
}


publish {
    artifactId = ARTIFACT_ID
    desc = 'A Gradle plugin to handle android application license notations'

    userOrg = metadata.organization
    groupId = metadata.groupId
    publishVersion = metadata.version
    website = metadata.website
    repository = metadata.repository
    licences = metadata.licences
}
