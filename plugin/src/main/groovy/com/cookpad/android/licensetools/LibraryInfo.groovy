package com.cookpad.android.licensetools

public class LibraryInfo implements Comparable<LibraryInfo> {

    String libraryName = ""

    ArtifactId artifactId;

    String filename = ""

    String year = "";

    String copyrightHolder = "";

    String notice = ""

    String license = ""

    boolean skip = false

    // from libraries.yml
    public static LibraryInfo fromYaml(Object lib) {
        def libraryInfo = new LibraryInfo()
        libraryInfo.artifactId = ArtifactId.parse(lib.artifact as String)
        libraryInfo.filename = lib.filename as String
        libraryInfo.year = lib.year as String
        libraryInfo.libraryName = lib.name as String
        if (lib.copyrightHolder) {
            libraryInfo.copyrightHolder = lib.copyrightHolder
        } else if (lib.authors) {
            libraryInfo.copyrightHolder = joinWords(lib.authors)
        } else if (lib.author) {
            libraryInfo.copyrightHolder = lib.author
        }
        libraryInfo.license = normalizeLicense(lib.license)
        libraryInfo.notice = lib.notice as String
        libraryInfo.skip = lib.skip as boolean
        return libraryInfo
    }

    // from dependency-license.xml generated by license plugin
    public static LibraryInfo fromXml(Object lib) {
        def libraryInfo = new LibraryInfo()
        libraryInfo.artifactId = ArtifactId.parse(lib.@name as String) // e.g. com.google.dagger:dagger:2.0
        libraryInfo.filename = lib.file.text() as String // e.g. dagger-2.0.jar
        libraryInfo.license = normalizeLicense(lib.license.@name[0] as String) // e.g. Apache 2.0
        return libraryInfo
    }

    private String guessNameFromArtifactId() {
        if (artifactId) {
            return artifactId.name.substring(0, 1).toUpperCase(Locale.ENGLISH) + artifactId.name.substring(1)
        } else {
            return null
        }
    }

    public String getName() {
        return libraryName ?: guessNameFromArtifactId() ?: filename
    }

    // called from HTML templates
    public String getCopyrightStatement() {
        if (notice) {
            return notice;
        } else if (!copyrightHolder) {
            return null;
        } else {
            return buildCopyrightStatement(copyrightHolder)
        }
    }

    private String buildCopyrightStatement(String copyrightHolder) {
        if (year) {
            return "Copyright &copy; ${year}, ${copyrightHolder}. All rights reserved."
        } else {
            return "Copyright &copy; ${copyrightHolder}. All rights reserved."
        }
    }


    static String normalizeLicense(String name) {
        switch (name) {
            case ~/(?i).*\bapache.*2.*/:
                return "apache2"
            case ~/(?i).*\bmit\b.*/:
                return "mit"
            case ~/(?i).*\bbsd\b.*\b2\b.*/:
                return "bsd_2_clauses"
            case ~/(?i).*\bbsd\b.*\b3\b.*/:
                return "bsd_3_clauses"
            case ~/(?i).*\bbsd\b.*\b4\b.*/:
                return "bsd_4_clauses" // not supported because it is a very legacy license
            case ~/(?i).*\bbsd\b.*/:
                return "bsd_3_clauses"
            default:
                return name
        }
    }

    static String joinWords(List<String> words) {
        if (words.size() == 0) {
            return ""
        } else if (words.size() == 1) {
            return words.first()
        } else {
            // example: a, b, c, and d
            String last = words.last()
            return words.subList(0, words.size() - 2).join(", ") + ", and " + last
        }
    }

    boolean equals(o) {
        if (this.is(o)) {
            return true
        }
        if (getClass() != o.class) {
            return false
        }

        LibraryInfo that = (LibraryInfo) o

        if (name != that.name) {
            return false
        }

        return true
    }

    int hashCode() {
        return name.hashCode()
    }

    @Override
    int compareTo(LibraryInfo o) {
        return name.compareToIgnoreCase(o.name)
    }
}
