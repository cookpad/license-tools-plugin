apply plugin: 'com.android.application'
apply plugin: 'com.cookpad.android.licensetools'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

repositories {
    google() //required for databinding
}

if (project.gradle.startParameter.taskNames.contains("checkLicenses")
        || project.gradle.startParameter.taskNames.contains("generateLicensePage")
        || project.gradle.startParameter.taskNames.contains("generateLicenseJson")
        || project.gradle.startParameter.taskNames.contains("check")) {
    // skip `kotlin-android-extensions` to ignore running `aar`s
    // https://github.com/JetBrains/kotlin/blob/d6d6cef10dec57aafb909e6dba763ca599a930ec/plugins/android-extensions/android-extensions-idea/src/org/jetbrains/kotlin/android/synthetic/idea/AndroidExtensionsProjectResolverExtension.kt#L94:30
} else {
    apply plugin: 'kotlin-android-extensions'
    androidExtensions {
        experimental = true
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "com.cookpad.android.licensetools.example"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath false
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    sourceSets {
        main.kotlin.srcDirs += 'src/main/java'
    }
}

licenseTools {
//    ignoredProjects = [
//            'example-dep'
//    ]
}

def implementation_project(arg) {
    if (project.gradle.startParameter.taskNames.contains("checkLicenses")
            || project.gradle.startParameter.taskNames.contains("generateLicensePage")
            || project.gradle.startParameter.taskNames.contains("generateLicenseJson")
            || project.gradle.startParameter.taskNames.contains("check")) {
        dependencies.implementation(dependencies.project(path: arg.path, configuration: 'default'))
    } else {
        dependencies.implementation(dependencies.project(path: arg.path))
    }
}

dependencies {
    implementation_project(path: ':example-dep')
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:25.3.1'
    implementation 'com.android.support:cardview-v7:25.3.1'
    implementation 'com.android.support:recyclerview-v7:25.3.1'
    implementation 'io.reactivex:rxandroid:1.1.0'
    implementation 'com.google.code.gson:gson:2.6.2'
    kapt 'com.github.gfx.android.orma:orma-processor:2.4.5'
    implementation 'com.github.gfx.android.orma:orma:2.4.5'
    kapt 'com.android.databinding:compiler:3.0.0'
    implementation 'com.google.android.gms:play-services-maps:8.4.0'
    implementation 'com.google.android.gms:play-services-location:8.4.0'
    implementation 'com.google.android.gms:play-services-analytics:8.4.0'
    implementation 'org.reactivestreams:reactive-streams:1.0.1-RC2'
    implementation 'com.twitter.sdk.android:twitter:3.1.1'
    releaseImplementation  'com.squareup.picasso:picasso:2.5.2'
    debugImplementation 'com.facebook.stetho:stetho:1.3.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
}
